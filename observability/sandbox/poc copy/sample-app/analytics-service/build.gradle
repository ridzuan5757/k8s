/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.6'
    id 'io.spring.dependency-management' version '1.1.4'
}


group = 'com.example.restservice'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.yaml') {
            details.useVersion '2.0'
        } else if (details.requested.group == 'org.apache.tomcat.embed') {
            details.useVersion '10.1.14'
            details.because('Fixes CVE-2023-44487')
        } else if (details.requested.group == 'ch.qos.logback') {
            details.useVersion '1.4.14'
            details.because('Fixes CVE-2023-6378')
        }
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

bootJar {
    mainClass = 'com.example.restservice.RestServiceApplication'
}

